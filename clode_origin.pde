//==================================================================================================
//   ____  __     __     _____    _   _  __     __    __  __   ______    _____    _   _    _____   |
//  |  _ \ \ \   / /    / ___ \  | \ | | \ \   / /   |  \/  | |  ____|  / ___ \  | \ | |  / ____|  |
//  | |_) | \ \_/ /    | |___| | |  \| |  \ \_/ /    | \  / | | |__    | |___| | |  \| | | (___    |
//  |  _ <   \   /     |  ___  | | . ` |   \   /     | |\/| | |  __|   |  ___  | | . ` |  \___ \   |
//  | |_) |   | |      | |   | | | |\  |    | |      | |  | | | |____  | |   | | | |\  |  ____) |  |
//  |____/    |_|      |_|   |_| |_| \_|    |_|      |_|  |_| |______| |_|   |_| |_| \_| |_____/   |
//                                                                                                 |
//==================================================================================================
//MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMN0dll:;;;;;;;;;cllldxOKNMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMWN0kd:..   ..''',;;,;:;'';;;cdOKWMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMNOd:,,;;'     ,;;;,,::;:c:;::,,,;:okKWMMMMMMMMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMMMMMMMMMMWXkc;;;;;:c:'    ;:::;,;cc::cc:c:;;:;,;co0WMMMMMMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMMMMMMMMWKdc:::ccc::lc,  ,;:cccc:;:c:;:lc:c:;::;;;;;o0WMMMMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMMMMMMWXkl;;:::cclc:cl; 'colcclll:;cc;,::;cc::c:,;;,,:kWMMMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMMMMMNk:;:;;:c::ccc::c:,':lolcllol:::'.,,,,::;cc;;;,,,;dXMMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMMMMXd;',:;;;;:;,:ll:cll::ldoc:clc,,      ;;;,:c:;:,';;:xNMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMMMKl,,,,;c:;clc::cllc:clccolcccc;;        '',;:;;c:,,;;ckWMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMMNd,',::clcc::loc:lddolllodc:::;;         :'',:;;:;,;,;:o0WMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMNd;::c:cc:ll:codddlclolclllllooc,         ,;;;:,;c:',,,;:xNMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMWO::c:;:c;'''',:clddlllodddlcccc:;';      ;,';cl;;;,,;,,::l0MMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMXoc:cl:;'       .;oxkxollooooodo:;:;';;;;;;::;::;c:,,;,,,,;kMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMOccllc,          .:ooodxxolllooolll:,:llc:c:;cl;,:;;;,';;;:kMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMxclc:;.          .;ddlloddoodoccclol:cl:;co:,::,:c;::,,',;:kMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMl::cc;.         ..;odooolcldxdolcll:coo;',,..,'':::c;,;',;c0MMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMo:ccc:'.       .',coloddoccoolclodl,''.        .,';c:;,,',oXMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMx::cclc,......',cdxdoloolcoxdlccc;'             .,;;:,',,:kWMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMO:clcloc:;;ccccoddxdooddc:cc;...                .':;;;',,oXMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMXo::cccoolldxdoooddodxdl,.                     ..';;;;,'cKMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMWOc::llclloollodxxxddl'                 ......''.,:;;;,:OWMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMWk:;::ccclodddloddoc.              ...','',,,;;';;;;,:0WMMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMMNd;:cclllccclodxxo.             .',,,;;,;:,,:;,;,,,c0WMMMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMMMNk:;:::clccclooo:.           .',,'';:,,cc;;;,,'.;dXMMMMMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMMMMW0ollcclc;;codo:.     ....''',,,',c:;:c;,:,,''l0WMMMMMMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMMMMMMNOl;,:c::cclllc'....'',,;,,:c;,;;''::,;,';o0WMMMMMMMMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMMMMMMMMNOocc::::clolc:lc:,;::;,;:;',;,',,',,ckXWMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMMMMMMMMMMWKxlc:::::ccclc:cc;,,;:;,;;,,,,:lxKNMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMWXOxolc:;,;:;;,',,''',;:ldx0XWMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMWN0dccc:,',;;,,:ldOXWMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
//==================================================================================================
//          _   _   ______    _____   ______    _____    _____    _____    _____   __     __       |
//         | \ | | |  ____|  / ____| |  ____|  / ____|  / ____|  / ___ \  |  __ \  \ \   / /       |
//         |  \| | | |__    | |      | |__    | (___   | (___   | |___| | | |__) |  \ \_/ /        |
//         | . ` | |  __|   | |      |  __|    \___ \   \___ \  |  ___  | |  _  /    \   /         |
//         | |\  | | |____  | |____  | |____   ____) |  ____) | | |   | | | | \ \     | |          |
//         |_| \_| |______|  \_____| |______| |_____/  |_____/  |_|   |_| |_|  \_\    |_|          |
//                                                                                                 |
//==================================================================================================                                                                              

// ------------------------------------------Main Features------------------------------------------
// > Main menu
// > Debug Menu
// > Feed (involvesPlayer function)
// > Different gamemodes
// > Simultaneous Games: 
// > 4 courses > 1 tourney
// > 4 different rankings
// > Top scoring players in each section + top scoring players overall go on to final match

// -----------------------------------Potential future mechanics------------------------------------
// > High Score (Double Double Double Double Double Bogey)
// > Cringe
// > Birds
// > Cult of the Hole
// > Boogey Tournies // High scores win // Boons + Curses
// > (DBB) Double bogey -> Hole in ones
// > (DBC) For all players: Par -> double bogey
// > Shadow games
// > Giant turtle (the course is on a giant turtle)
// > Balls
// > Clubs (both the sticks and the bougie places)
// > Tourny of the Damned (Revive player?)
// > Sainthood
// > Strikes

// ----------------------------------------------Bugs-----------------------------------------------
// Fix rewinding
// Hole scale sometimes breaks? No clue why lmao
//
//==================================================================================================


LeagueManager leagueManager = new LeagueManager();
PlayerManager playerManager = new PlayerManager();
TourneyManager tourneyManager;
Feed feed = new Feed();
UIController uiController = new UIController();

static final boolean statCollection = false;
boolean firstFrame = true;
int eventsPerFrame = 1000;
HashMap<Float,Integer> sizeAndCalculatedPar = new HashMap<Float,Integer>();
HashMap<Float,Integer> sizeAndMedianPar = new HashMap<Float,Integer>();
HashMap<Float,Float> sizeAndMeanPar = new HashMap<Float,Float>();

int totalPlayers = statCollection ? 1000 : 96;
int playersPerTourney = statCollection ? 100 : 16;
int coursesPerTourney = 4;
int holesPerCourse = statCollection ? 18 : 9;

PFont boldFont;
PFont font;

int timePassed = 0;
int speedValue = 1000;
boolean playActive = false;
boolean timeStopped = false;

Button homeButton;
Button[] timeButtons = new Button[5];
Button[] headButtons = new Button[4];
UIComponent[][] uiComponents = new Button[6][];
VariableDisplayer variableDisplayer;
EventDisplayer eventDisplayer;
HoleDisplayer holeDisplayer;

// Setup
void setup() {
  //Application Settings
  surface.setTitle("Glolf!");
  //surface.setResizable(true);
  surface.setIcon(loadImage("assets/icons/game_icon/bogey2.png"));
  size(1800, 960);
  
  //Font
  boldFont = loadFont("assets/fonts/pixeloperator/PixelOperator-48.vlw");
  font = loadFont("assets/fonts/pixeloperator/PixelOperator-48.vlw");
  
  //Initialize Players & TourneyManager
  playerManager.clearAllPlayers();
  playerManager.addNewPlayers(totalPlayers);
  tourneyManager = new TourneyManager(new Tourney(playerManager.chooseRandomLivingPlayers(playersPerTourney), coursesPerTourney, holesPerCourse));  
    
  // Initialize Buttons & Displays
  uiComponents = uiController.uiStartUp();
  
  homeButton        = (Button)uiComponents[0][0];
  timeButtons       = (Button[])uiComponents[1];
  headButtons       = (Button[])uiComponents[2];  
  eventDisplayer    = (EventDisplayer)uiComponents[3][0];
  holeDisplayer     = (HoleDisplayer)uiComponents[4][0];
  variableDisplayer = (VariableDisplayer)uiComponents[5][0];
}

// Draw
void draw() {
  if (!statCollection) background(200);
  
  textFont(boldFont);
  for (int i = 0; i < 3; i++) {
    for (UIComponent component : uiComponents[i]) component.display();
    if (i == 2) {
      textFont(font);
      strokeWeight(2);
      stroke(0);
      line(0, uiController.buttonSetHeight, width, uiController.buttonSetHeight);
    }
  }

  variableDisplayer.display();
  eventDisplayer.display();
  if (!statCollection) holeDisplayer.display();
  else if (firstFrame) {
    holeDisplayer.display();
    firstFrame = false;
  }
  
  if (statCollection) {
    for (int i = 0; i < eventsPerFrame; i++) {
      if (feed.lastEvent() instanceof EventTourneyConclude) {
        tourneyManager.newRandomTourney(playerManager.chooseRandomLivingPlayers(playersPerTourney), coursesPerTourney, holesPerCourse);
      }
      nextEvent();
    }
  }
  else if (playActive && !timeStopped) {
    timeButtons[0].unpress();
    timeButtons[1].press();
    timeButtons[2].disable();
    timeButtons[3].disable();
  
    if (millis() >= timePassed + speedValue) nextEvent();
  }
  else if (!timeStopped) {
    timeButtons[0].press();
    timeButtons[1].unpress();
    timeButtons[2].enable();
    timeButtons[3].enable();
  }
}

void stopTime() {
  timeStopped = true;
  for (Button b : timeButtons) b.disable();
}
void resumeTime() {
  timeStopped = false;
  for (Button b : timeButtons) b.enable();
}

GlolfEvent nextEvent() {
  GlolfEvent lastEvent = leagueManager.nextEvent();
  for (int i = 0; i < 100; i++) {
    if (lastEvent != null) break;
    else lastEvent = leagueManager.nextEvent();
  }
  if (lastEvent == null) lastEvent = new EventNoEvent();
  
  timePassed += speedValue;
  feed.addEvent(lastEvent);
  println(lastEvent.toText());
  return lastEvent;
}

// Picks a random item from .txt file
String generateRandomFromList(String filename) {
  String[] list = loadStrings(filename);
  int idx = int(random(list.length));
  return list[idx];
}

// When keys are pressed
void keyPressed() {
  switch(key) {
    case ' ':
      playActive = !playActive;
      break;
    case 'c':
      if (timeStopped) tourneyManager.newRandomTourney(playerManager.chooseRandomLivingPlayers(playersPerTourney), coursesPerTourney, holesPerCourse);
      break;
  }
}

// When mouse is pressed
void mousePressed() {
  Button[] allButtons = (Button[])concat(concat(timeButtons, headButtons), append(holeDisplayer.butts, homeButton));
  
  for (Button button : allButtons) {
    if (button.isOver() && button.enabled) {
      button.press();
      switch(button.onClick) {
        case "pause":
          playActive = false;
          break;
        case "play": 
          if (!playActive) timePassed = millis();
          playActive = true;
          break;
        case "back":
          if (feed.everyEvent.size() > 1) {
            tourneyManager.undoEvent(feed.lastEvent());
            feed.removeLastEvent();
          }
          break;
        case "next":
          nextEvent();
          break;
        case "speed": 
          if (speedValue == 1000) speedValue = 500;
          else if (speedValue == 500) speedValue = 1;
          else if (speedValue == 1) speedValue = 1000;
          break;
        case "show_feed": break;
        case "debug_menu": break;
        case "girl":
          println("*kisskisskisskisskisskisskisskisskisskiss*");
          break;
        case "save_players":
          playerManager.savePlayersToJSON();
          break;
        case "kill_player":
          if (feed.lastEvent().playState().tourney != null && !timeStopped) {
            leagueManager.killPlayerDuringTourney();
            nextEvent();
          }
          break;
        case "restart":
          tourneyManager.restartTourney();
          break;
        case "continue":
          tourneyManager.newRandomTourney(playerManager.chooseRandomLivingPlayers(playersPerTourney), coursesPerTourney, holesPerCourse);
          break;
        case "exit":
          exit();
          break;
        default: break;
      }
    }
  }
  
  for (ButtonChangeVarDisplay button : variableDisplayer.displayChangeButtons) {
    if (button.isOver()) {
      button.onClick();
      for (ButtonChangeVarDisplay b : variableDisplayer.displayChangeButtons) {
        if (b != button) b.deactivate();
      }
    }
  }
  
  if (variableDisplayer.isOverInfo()) variableDisplayer.selectPlayer();
}

// When mouse is released
void mouseReleased() {
  homeButton.unpress();
  for (Button button : timeButtons) {
    if (button.onClick != "play" && button.onClick != "pause") button.unpress();
  }
  for (Button button : headButtons) button.unpress();
  for (Button button : holeDisplayer.butts) button.unpress();
}

// When mouse is moved
void mouseMoved() {
  Button[] allButtons = (Button[])concat(concat(timeButtons, headButtons), append(holeDisplayer.butts, homeButton));
  for (Button button : allButtons) {
    if (button.isOver()) button.select();
    else button.deselect();
  }
  
  if (!variableDisplayer.isOverInfo()) variableDisplayer.dehover();
}

// When mouse is dragged
void mouseDragged() {
  Button[] allButtons = (Button[])concat(concat(timeButtons, headButtons), append(holeDisplayer.butts, homeButton));
  for (Button button : allButtons) {
    if (button.isOver()) button.select();
    else button.deselect();
  }
  
  if (!variableDisplayer.isOverInfo()) variableDisplayer.dehover();
}

// When mouse wheel is scrolled
void mouseWheel(MouseEvent e) {
  variableDisplayer.scroll(e.getCount());
}

// saved for posterity
// GLOLF TO DO LIST
// No. of players - no set amount (start w 12)
// Players: name, gender (random adjectives), cringe (chance of total beefitude), dumbassery (choice of stroke type), yeetness (strength), trigonometry (accuracy),
//          bisexuality (curve skill), asexuality (hole-in-one chance), scrappiness (skill in rough areas), charisma (get it in the hole ;3), autism (magic)
// Strokes: drive (max length min accuracy), approach (medium to long range + more accuracy), chip (med-short range), putt (short range)
// Holes: par, roughness, heterosexuality (straightness), thicc (likelihood to go oob), verdancy (easiness to get on the green),
//          obedience (green tameness), quench (water hazards), thirst (sand bunkers)
// Tourney: 18 courses of stroke play, sudden death on tie
